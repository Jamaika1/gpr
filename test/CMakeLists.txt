option(BUILD_TESTING "Run tests to verify gpr lib" OFF)

if(BUILD_TESTING)
  enable_testing()

  if(MSVC)
    add_custom_target(tests "${CMAKE_CTEST_COMMAND}" -C $<$<CONFIG:Debug>:Debug>$<$<CONFIG:Release>:Release> -V -T Test VERBATIM)
  else()
    add_custom_target(tests "${CMAKE_CTEST_COMMAND}" -V -T Test VERBATIM)
  endif()
  set_target_properties(tests PROPERTIES FOLDER "Tests")

  set(TEST_DATA_DIR ${CMAKE_CURRENT_SOURCE_DIR}/../data/samples)
  file(TO_NATIVE_PATH "${TEST_DATA_DIR}" TEST_DATA_DIR)

  add_executable(test_image test_image.cpp ${LIB_COMMON_BASE_DIR}/../googletest/src/gtest-all.cc)
  set_target_properties(test_image PROPERTIES FOLDER "Tests")
  target_include_directories(test_image PRIVATE ${LIB_COMMON_BASE_DIR}/../googletest ${LIB_COMMON_BASE_DIR}/../googletest/include ${LIB_GPR_INCLUDE_DIRS} ${LIB_COMMON_INCLUDE_DIRS} ${LIB_DNG_INCLUDE_DIRS})
  if(NOT WIN32)
    set_target_properties(test_image PROPERTIES RUNTIME_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})
  endif()
  target_link_libraries(test_image gpr)
  if(MSVC)
    set_target_properties(test_image PROPERTIES VS_DEBUGGER_COMMAND_ARGUMENTS ${TEST_DATA_DIR})
  endif()
  add_test(NAME test_image COMMAND test_image ${TEST_DATA_DIR})
  add_dependencies(tests test_image)
  if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/../data/samples/Hero6/GOPR0024.GPR")
    target_compile_definitions(test_image PRIVATE -DTEST_HERO6="GOPR0024.GPR")
  endif()
  if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/../data/samples/HERO7/GOPR9231.GPR")
    target_compile_definitions(test_image PRIVATE -DTEST_HERO7="GOPR9231.GPR")
  endif()
  if(EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/../data/samples/HERO8/GOPR0129.GPR")
    target_compile_definitions(test_image PRIVATE -DTEST_HERO8="GOPR0129.GPR")
  endif()
endif()
